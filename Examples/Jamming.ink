=== Intro ===
// Black screen
Graham: At the brink. That's the theme? #audio = Graham01
Graham: Shouldn't it be on the brink? At the brink, on the brink, is there a difference? #audio = Graham02
Graham: Worry about that later, get started. #audio = Graham03
Graham: Title screen.  #audio = Graham04
// A title screen appears, there's no title just Start and Quit options.
-> END

=Title
Graham: What'll I call it? Doesn't matter. #audio = Graham05
-> END

=Quit
Graham: I'll implement this later, sorry you're along for the ride.  #audio = Graham06
-> END

=== Brainstorm ===
// Cut to a whiteboard
Graham: Ok come up with some ideas. At the brink, what does that mean? Google. #audio = Graham07
Graham: On the verge of doing something or something happening, like I'm at the brink of starting a game. #audio = Graham08
Graham: I need an 'of something'. #audio = Graham09
// The choices become a mind map.
-(opts)
* Graham: At the brink of death. #visible Death #audio = Graham10
    Graham: A story about someone on life support and their regrets, played out in conversations with visitors. #audio = Graham11
    Graham: Can people on life support talk? Don't they have that tube thing in their mouth. Maybe a terminal illness then, ugh this is depressing me before I've made it. #audio = Graham12
    -> opts
* (Madness) Graham: At the brink of madness. #visible Madness #audio = Graham13
    Graham: Everyone's going to do that, extra points if it's Lovecraftian. Next. #audio = Graham14
    -> opts
* Graham: At the brink of extinction. #visible Extinction #audio = Graham15
    Graham: Following a comet strike on the Earth, a family of dinosaurs struggle to adapt to the new world, scrap that, it reminds me of that awful Disney film. #audio = Graham16
    -> opts
* Graham: At the brink of tears. #visible Tears #audio = Graham17
    Graham: A capable young woman moves to London to enter the world of work. Bright and optimistic, she soon finds frustration as male colleagues undermine, harass, and objectify her.  #audio = Graham18
    Graham: Not sure how this is a game. Maybe some kind of bottling of emotions, let's put it in the maybe pile. #audio = Graham19
    -> opts
* {Madness} Graham: At the brink of collapse. #visible Collapse #audio = Graham20
    Graham: Like the environment, that's actually quite pertinent, those climate change protests were going on today. I was stuck in traffic, ironically creating more CO2 emissions.  #audio = Graham21
    -> Collapse 
* Graham: At the brink of starvation. #visible Starvation #audio = Graham22
    Graham: A shipwrecked man on desert island with no resources must wait for rescue. A game about waiting, that'll be fun. #audio = Graham23
    -> opts 
* Graham: At the brink of survival. #visible Survival #audio = Graham24
    Graham: There's lots of survival games, so this has promise. No zombies though! #audio = Graham25
    Graham: A downed helicopter pilot in enemy territory must survive with limited resources while soldiers search for him. Hmm unless I can narrow it down, it's a bit too ambitions for a three day game jam.   #audio = Graham26
    ->opts
* Graham: At the brink of destruction. #visible Destruction #audio = Graham27
    Graham: The world's major powers declare nuclear war on each other, I loved that Amiga game, I wonder if I still have it. There was no real narrative though, it's not really suitable for this jam.  #audio = Graham28
    -> opts

=Collapse
Graham: Let's explore this one. #audio = Graham29
Graham: The environment is on the verge of collapse, scientists have told us so for a long time now. #audio = Graham30
Graham: People deny it though, I don't think any scientists do, not reputable one's anyway. #audio = Graham31
Graham: Who'd be the protagonist? A protester on their bike? An oil baron? Mother nature? Who needs to change. Mother nature won't change, does that mean she's the antagonist? We need to change to prevent mother nature from destroying us. Destroying us as a reaction to our folly. #audio = Graham32
Graham: Why are we as a species moving forward with our own destruction. Greed, denial, stupidity. A combination of those things, then why are we those things? #audio = Graham33
Graham: I've got it, I need to start at the beginning, we didn't one day decide to destroy the environment. Bit by bit we bent mother nature to our will, we got here gradually.  #audio = Graham34
Graham: Now we're carrying on like always because that was what worked in the past, that's what was rewarding, so we're stuck in this pattern of behaviour. How do we collectively realise that and make the change. #audio = Graham35
Graham: To make this game I just need to unpack the entire human condition. This game needs to be about everything. #audio = Graham36
-> END 
=== Title2 ===
Graham: First, title screen. #audio = Graham37
Graham: I'll put a placeholder title for now.  #audio = Graham38
-> END

=Quit
Graham: I'll sort quit out later, I'm on a roll. #audio = Graham39
-> END 

=== CaveManDays ===
// Showing a scene of cave couple, the cave man is rubbing sticks.
Graham: We first begin in cave man days, that's not the proper term, make a note to research the setting later.  #audio = Graham40
Graham: They are discovering fire for the first time, enabling them to overcome the cold and keep predators at bay. #audio = Graham41
Graham: Wait, what the hell am I doing, I've only got two and a half days left and I'm trying to get the whole of human history into a game. #audio = Graham42
Graham: Think. Narrow it down. One person, and what they can do on the individual level. #audio = Graham43
Graham: Like those protesters, they were doing their bit. You even admired them as you sat in your car creating more emissions. You weren't going anywhere you could've turned your engine off.  #audio = Graham44
-> END

=== Title3 ===
Graham: Okay, title screen. Car game placeholder. #audio = Graham45
-> END

=Quit
Graham: Quit doesn't work, get over it.  #audio = Graham46
-> END

=== CarGameIntro ===
Graham: You're trying to get to the Bristol Hippodrome to watch the Lion King, an accident on the M4 meant you were already running behind, but now you're in heavy traffic. #audio = Graham47
Graham: Climate change protesters are bringing the city centre to a standstill, you need to control your emissions as you slowly move forward. #audio = Graham48
Graham: Everytime you stop, switch off the engine. #audio = Graham49
*[Play] -> END
*[Skip] #toScene Phone
-> END

=== AfterLionKing ===
// Cut to outside the Bristol Hippodrome, Player is waiting outside.
Graham: You watched Lion King, it was great but you're aware that inkJam 2019 has already started. You wonder what the theme is. #audio = Graham50
* [Check itch.io]#visible PhoneOn
    Graham: At the brink, how is that a theme? It's more like a writing prompt. #audio = Graham51
    Graham: I have to drive back to Exeter now anyway, I'll use that time to come up with some ideas. Then I'll get some sleep and start in the morning.  #audio = Graham52
    -> END
    

=== Drive ===
// Cut to top down driving, three lanes with coffee cups in the road.
Graham: It's late, I'll need coffee. #audio = Graham53
Graham: I'll drive into the cups to collect the precious elixir of alertness.  #audio = Graham54
-> END

=FirstCup
// After the first cup you realise the prototype is environmentally unfriendly
Graham: These are paper cups, I don't know why they can't be recycled, but this game is about the environment!  #audio = Graham55
-> END

=Mugs
// The paper cups transform into mugs of coffee
Graham: Okay whatever, actual mugs of coffee will do.  #audio = Graham56
-> END

=== Home ===
// Home, Player goes straight to bed
Graham: Sleep, start the game in the morning. #audio = Graham57
*[Sleep]
*[Lie awake thinking about game ideas]
- Graham: I guess we're just gonna think about game ideas and this theme. Who starts a game jam at night, that's so cruel. They know we won't be able to sleep, all that creative energy is at a maximum now. #wait 3.0 #audio = Graham58
Graham: Paul even put in the email don't forget to get some sleep, that arsehole knew exactly what he was doing!  #audio = Graham59
-> END

=Start
// Awake at his laptop, player starts to work on the game
Graham: At the brink of environmental collapse, that's my game, that's the theme, but what's the story? This is a narrative game jam after all. #audio = Graham60
Graham: I'll look at other environmental stories and see if they can give me some ideas. #audio = Graham61
-(opts)
* [Princess Mononoke]
    Graham: The clash of civilisation and nature, the humans exploit the resources of mother nature. Mother nature fights back. Eboshi desires to destroy the forest, she goes on a hunting expedition to kill the Forest Spirit. #audio = Graham62
    Graham: When it's decapitated, ooze comes out destroying everything it touches. Mother nature's revenge for humankind's greed.  #audio = Graham63
    -> opts
* [Avatar]
    Graham: We come to another planet to mine for minerals, but there's a civilisation that lives in harmony with nature opposing us. Jake becomes an avatar living among them, like some kind of hearts and minds mission.  #audio = Graham64
    Graham: Jake learns that there is another way of living that doesn't have to be destructive and unsustainable, and that's worth protecting.   #audio = Graham65
    -> opts
* [Happy Feet]
    Graham: A penguin named Mumble is born, due to an accident he can't do the mating call of penguins, but he can tap dance. Ridiculed by his elders, the conventional wisdom, he feels isolated and eventually ends up in captivity. #audio = Graham66
    Graham: Mumble becomes popular with his dancing, and is set free with a tracking device. Humans come to his home and realise their actions have caused harm to the penguins and change their ways. #audio = Graham67
    Graham: We learn that new and different voices need to be listened to if we are going to save the future.  #audio = Graham68
    -> opts
* ->
Graham: What am I doing? It's night time already and you've just been going down all these rabbit holes. You even watched Happy Feet for the first time so you'd have something to write, all because you didn't want two Ghibli films in the options, but that would've been fine. #audio = Graham69
Graham: I don't know if I can finish this, it's okay no one will notice if you don't submit anything. Just put a heroic message on Discord about how the scope got out of control, you learned so much and are looking forward to next year. #audio = Graham70
Graham: Go to sleep, we'll have ice cream tomorrow. #audio = Graham71
Dog: Feeeed meeee. #wait 1.5 #audio = Dog1
-> END

=== Dream ===
// Car honking sound wakes Graham up.
Graham: Ugh, who's doing that?  #audio = Graham72
-> END

=GetIn
// A man pulls a gun on Graham.
Man: Get in the car.  #audio = Man1
-> END
=InCar
// In the car we se a meditative figure.
Graham: You're Jon Ingold. #audio = Graham73
Jon: No, I'm his next form, Zen Jon Ingold. #audio = Jon01
Graham: You don't sound like him. #audio = Graham74
Jon: That's because you were too shy to ask him to voice himself. Instead you asked Joe, good old dependable Joe, he'll come through for you, even though it's the weekend and he's got better things to be doing. Nice one! #audio = Jon02
Graham: At least it's not a newsreader. #audio = Graham75
Jon: You ask him to do that later! #audio = Jon03
Graham: Why am I here Zen Jon Ingold? #audio = Graham76
Jon: You need help. #audio = Jon04
Graham: My game, inkJam. I don't know what to do you've got to save me. #audio = Graham77
Jon: Tell me about your game. #audio = Jon05
*   Graham: It's about the environment[] and how it's on the brink of collapse, and well, I don't really know how to make that into a game. #audio = Graham78
*   Graham: It doesn't matter, just give me some good advice. #audio = Graham79
- Jon: Wrong. That was two choices, yes and no. The player won't like that, don't you agree, tell me why? #audio = Jon06
*   Graham: Because they feel there's a right or wrong[] answer and it's not about choosing your story but passing a quiz. #audio = Graham80
*   Graham: No! Who cares! #audio = Graham81
*   Graham: Why are you asking me this? It has nothing to do with my game. #audio = Graham82
    Jon: Indulge me. #audio = Jon07
    Graham: The player is always going to want to pick the correct answer. #audio = Graham83
- Jon: Nice try but that was just yes, no, and no but yes.  #audio = Jon08
Jon: Why do you want to make a game about the environment? #audio = Jon09
Graham: Because it's important, it's precious and it's relevant. #audio = Graham84
Jon: But where's the game in all that? #audio = Jon10
Graham: I have some goofy prototypes, I did a lot of research, but it's a mess and I'm running out of time. #audio = Graham85
Jon: Just dazzle them in the end, finish strong and the player will love it, and don't be afraid to make stuff up, after all it's supposed to be entertaining. #audio = Jon11
Graham: You're just quoting Adaption with Nicolas Cage! #audio = Graham86
Jon: That's the kind of game you wanted to make in the first place, wasn't it?  #audio = Jon12
-> END

=== WakeUp ===
// New title screen appears
Graham: I've got it! Title screen. #audio = Graham87
Graham: In a Jam. #audio = Graham88
-> END

=Quit
Graham: Too late to quit, come along. #audio = Graham89
-> END

=== Intro2 ===
// Flash through the game so far, end up 1st person driving.
Graham: Game starts when inkJam starts, we see the theme, at the brink.  #audio = Graham90
-> END
=Continue
Graham: Cut to brainstorming session, imagined prototypes, our game developer is anxious to get started. But, we've got the long midnight drive back to Devon. #audio = Graham91
-> END 

=Radio

Newsreader: Reports of a giant tentacled monster are coming out Exeter this evening. The beast is beached over the whole of Exmouth and its tentacles are striking out at Exeter and nearby villages. Mass casualties are expected. #audio = Newsreader01
Newsreader: Experts are calling this the coming of Cthulhu, some speculating that pollution of the seas has caused him to act and seek revenge on us all. #audio = Newsreader02
Newsreader: The government warns against all unnecessary travel and to remain in your homes, but in this newsreader's humble opinion, run! Get away from Cthulhu as fast as you can!  #audio = Newsreader03
// A tentacle wipes out the car, fade to black.
-> END


=== Home2 ===
// Looking crazed Graham has just finished making the game.
Graham: It's done! #audio = Graham92
* [Press release game button]
-> END

